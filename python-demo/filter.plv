import prelude;
import qr;
import util;
import operations;

make_filter_state () :: filter_state := (
  s :: filter_state;
  s.invalid <- True;
  s.num_sats <- 0;
  s;
);

kalman_init_
  (out state :: filter_state)
  (ref :: gnss_signal_t)
  :: () := (
  printf "reset filter. ref: "; print_sat ref; nl ();
  state.num_sats <- 1;
  state.ref <- ref;
  state.invalid <- False;
  state.x <- 0.0;
  position_covariance :: double[state.num_sats + 2, state.num_sats + 2];
  position_covariance <- 0.0; -- TODO
  state.P <- position_covariance;
);

kalman_predict_ {dim}
  (x :: double[dim])
  (P :: double[dim, dim])
  (F :: double[dim, dim])
  (Q :: double[dim, dim])
  (out x_new :: double[dim])
  (out P_new :: double[dim, dim])
  :: () := (
  P_new <- F * P * F^T + Q;
  x_new <- F * x;
);

kalman_update_ {xdim, dim}
  (x :: double[xdim])
  (P :: double[xdim, xdim])
  (y :: double[dim])
  (H :: double[dim, xdim])
  (R :: double[dim, dim])
  (out x_new :: double[xdim])
  (out P_new :: double[xdim, xdim])
  :: () := (

  innovation := y - H * x;
  PHt := P * H^T;
  S := H * PHt + R;
  K := PHt * S^(-1) :: double[xdim, dim];

  x_new <- x + K * innovation;
  P_new <- (eye () - K * H) * P;
);

omega_dot_unit_vector {sats, dim}
  (base_pos :: double[dim])
  (sat_positions :: double[sats, dim])
  (estimate :: double[dim])
  :: double[sats, dim] := (

  vec i in sats -> (
    h := sat_positions[i] - base_pos;
    omega := norm h + norm (h - estimate);
    (2*h - estimate) / omega
  );
);

observation_model_ {sats}
  (pseudoranges :: double[sats])
  (carrier_phases :: double[sats])
  (x :: double[3+sats])
  (base_pos :: double[3])
  (sat_positions :: double[sats, 3])

  (sig_cp :: double)
  (sig_pr :: double)

  (out y :: double[2*(sats-1)])
  (out H :: double[2*(sats-1), 3+(sats-1)])
  (out R :: double[2*(sats-1), 2*(sats-1)])
  :: () := (

  E :: double[sats, 3];
  E <- (omega_dot_unit_vector base_pos sat_positions x[0:3]) / GPS_L1_LAMBDA;

  u :: double[sats];
  u <- -1.0 / sqrt sats;
  u[0] <- u[0] + 1;

  P_bar :: double[sats-1, sats];
  P_bar <- (eye() - 2 * (u * u^T) / (u * u))[1:];

  F := eye{sats-1} - (1 / (sats - sqrt sats));

  sig_ratio := sig_cp / (sig_pr / GPS_L1_LAMBDA);

  -- Outputs
  y <- (P_bar * carrier_phases) #
         (sig_ratio * P_bar * pseudoranges / GPS_L1_LAMBDA);

  -- block: [PE, F; sig_ratio*PE, 0]
  PE := P_bar * E;
  H <- ((PE^T # F^T)^T # ((sig_ratio * PE^T) # zero())^T);

  R <- 4 * sig_cp * eye();
);

update {dim}
  (inout state :: filter_state)
  (sdiffs :: measurement[dim])
  -- ? (receiver_ecef :: double)
  -- ? (raim flag)
  :: () := (

  if dim == 0 then (
    printf "empty filter\n";
    state <- make_filter_state ();
    return;
  );
  if state.invalid then kalman_init_ (out state) sdiffs[0].sat_id;

  new_sats := vec i in dim -> sdiffs[i].sat_id;

  if not (contains_sat state.ref new_sats) then (
    index := choose_ref state sdiffs;
    if index < 0 then (
      kalman_init_ (out state) sdiffs[0].sat_id;
    ) else (
      printf "change ref: ";
      print_sat sdiffs[index].sat_id; nl ();
      change_ref (inout state) sdiffs[index].sat_id;
    );

  );
  while (drop_a_sat (inout state) new_sats);

  for i in dim -> (
    if not (contains_sat new_sats[i] state.sats or sid_is_equal new_sats[i] state.ref) then
      add_sat (inout state) new_sats[i]
  );

  assert state.num_sats == dim;
  for i in dim -> assert (
    contains_sat sdiffs[i].sat_id state.sats or
    sid_is_equal sdiffs[i].sat_id state.ref
  );

  -- TODO kalman_update
);
